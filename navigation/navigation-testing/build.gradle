/*
 * Copyright 2018 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.PlatformIdentifier
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
}

androidXMultiplatform {
    android()
    desktop()
    mac()
    linux()
    ios()
    js()
    wasm()

    defaultPlatform(PlatformIdentifier.ANDROID)
}

kotlin {
    js {
        browser()
    }
    wasmJs()

    watchosArm64()
    watchosArm32()
    watchosX64()
    watchosSimulatorArm64()
    tvosArm64()
    tvosX64()
    tvosSimulatorArm64()

    sourceSets {
        commonMain {
            dependencies {
                api(project(":navigation:navigation-runtime"))
                api(project(":lifecycle:lifecycle-runtime-testing"))
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinTest)
                implementation(libs.kotlinCoroutinesTest)
                implementation(project(":internal-testutils-navigation"))
                implementation(project(":kruth:kruth"))
            }
        }

        androidUnitTest {
            dependsOn(commonTest)
        }

        androidInstrumentedTest {
            dependencies {
                implementation(project(":internal-testutils-navigation"))
                implementation(libs.testCore)
                implementation(libs.testExtJunit)
                implementation(libs.testExtTruth)
                implementation(libs.testRunner)
                implementation(libs.testRules)
                implementation(libs.truth)
            }
        }

        jbMain.dependsOn(commonMain)
        jbTest.dependsOn(commonTest)

        targets.all { target ->
            if (target.platformType !in [
                    KotlinPlatformType.androidJvm,
                    KotlinPlatformType.common
            ]) {
                target.compilations["main"].defaultSourceSet {
                    dependsOn(jbMain)
                }
                target.compilations["test"].defaultSourceSet {
                    dependsOn(jbTest)
                }
            }
        }
    }
}

androidx {
    name = "Navigation Testing"
    publish = Publish.SNAPSHOT_AND_RELEASE
    inceptionYear = "2017"
    description = "Android Navigation-Testing"
    metalavaK2UastEnabled = true
}

android {
    namespace "androidx.navigation.testing"
}
